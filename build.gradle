plugins {
    id 'application'
    id 'org.hidetake.ssh' version '2.10.1'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url('https://frcmaven.wpi.edu/artifactory/release')
    }
}

remotes {
    target {
        host = "$DEPLOY_HOST"
        user = "$DEPLOY_USER"

        if (project.hasProperty('targetPassword')) {
            password = project.property('targetPassword')
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: project.file('libs'))

    implementation "com.google.code.gson:gson:2.8.5"
    implementation ("com.flash3388.flashlib:flashlib.vision.cv:$FLASHLIB_VERSION") {
        exclude group: 'org.openpnp'
    }
    implementation "com.flash3388.flashlib:flashlib.frc.nt:$FLASHFRC_VERSION"

    implementation group: "edu.wpi.first.thirdparty.frc${YEAR}.opencv", name: 'opencv-java', version: "$OPENCV_VERSION"
    implementation group: "edu.wpi.first.thirdparty.frc${YEAR}.opencv", name: 'opencv-jni', version: "$OPENCV_VERSION", classifier: "$REMOTE_ARCH"

    implementation "edu.wpi.first.wpiutil:wpiutil-java:$WPILIB_VERSION"
    implementation "edu.wpi.first.cscore:cscore-java:$WPILIB_VERSION"
    implementation "edu.wpi.first.ntcore:ntcore-java:$WPILIB_VERSION"
    implementation "edu.wpi.first.cameraserver:cameraserver-java:$WPILIB_VERSION"
    implementation group: 'edu.wpi.first.cscore', name: 'cscore-jni', version: "$WPILIB_VERSION", classifier: "$REMOTE_ARCH"
    implementation group: 'edu.wpi.first.ntcore', name: 'ntcore-jni', version: "$WPILIB_VERSION", classifier: "$REMOTE_ARCH"

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.4.2'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.2.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.23.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.1'
}

test {
    useJUnitPlatform()

    testLogging {
        events "failed"
        exceptionFormat "full"
        showStackTraces true
    }
}

application {
    mainClassName = 'frc.team3388.vision.Main'
    applicationName = 'frcvision'
}

run {
    args project.file('frc.json').absolutePath
}

task deploy {
    dependsOn tasks.build
    doLast {
        ssh.run {
            session(remotes.target) {
                def distributionName = "${application.applicationName}.zip"

                // delete old version
                execute "rm -rf ${DEPLOY_PATH}/${application.applicationName}"

                // upload new code
                put from: "${project.buildDir}/distributions/${distributionName}", into: "$DEPLOY_PATH"
                execute "unzip ${DEPLOY_PATH}/${distributionName}"
            }
        }
    }
}

task runRemote {
    dependsOn tasks.deploy
    doLast {
        ssh.run {
            session(remotes.target) {
                execute "cd ${DEPLOY_PATH}/${application.applicationName}"
                execute "./bin/${application.applicationName}"
            }
        }
    }
}
